// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //zero x?
    Mux16(a=x, b=false, sel=zx, out=tempx);
    //negate x?
    Not16(in=tempx, out=nottempx);
    Mux16(a=tempx, b=nottempx, sel=nx, out=tempx2);
    //zero y?
    Mux16(a=y, b=false, sel=zy, out=tempy);
    //negate y?
    Not16(in=tempy, out=nottempy);
    Mux16(a=tempy, b=nottempy, sel=ny, out=tempy2);
    //compute f
    //f == 1 sets out = x+y
    Add16(a =tempx2, b =tempy2, out = xplusy);
    //f == 0 sets out = x&y
    And16(a=tempx2, b=tempy2, out=xandy);
    Mux16(a=xandy, b=xplusy, sel=f, out=tempout);

    //negate the ouptut?
    Not16(in=tempout, out= tempout2);
    Mux16(a=tempout, b=tempout2, sel=no, out[0..7]=first, out[8..15]=second, out[15]=ng, out=out);

    Or8Way(in=first, out=first8);
    Or8Way(in=second, out=back8);
    Or(a= first8, b=back8, out=sel);
    Mux(a=true, b=false, sel=sel, out=zr);
    
}